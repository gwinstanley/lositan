buildscript {
	if (!JavaVersion.current().java8Compatible)
		throw new IllegalStateException("Build requires JDK 8+ (detected ${JavaVersion.current()})")
}
import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.api.file.RelativePath
plugins {
	id 'application'
}
repositories {
	mavenLocal()
	mavenCentral()
	ivy {
		url 'https://github.com/'
		patternLayout {
			artifact '/[organisation]/[module]/archive/[revision].[ext]'
		}
		metadataSources {
			artifact()
		}
	}
}
ext {
	ver_biopython = '162'
	ver_batik = '1.11'
	ver_itext = '5.5.13.1'
	ver_jfreechart = '1.5.0'
	ver_jython = '2.7.1'
}
configurations {
	biopython
}
dependencies {
	biopython "biopython:biopython:biopython-${ver_biopython}@zip"
	implementation "org.jfree:jfreechart:${ver_jfreechart}"
	implementation "org.python:jython-standalone:${ver_jython}"
	implementation "com.itextpdf:itextpdf:${ver_itext}"

	implementation "org.apache.xmlgraphics:batik-awt-util:${ver_batik}"
	implementation("org.apache.xmlgraphics:batik-dom:${ver_batik}") {
		exclude group: 'xalan', module: 'xalan'
		exclude group: 'xml-apis', module: 'xml-apis'
	}
	implementation("org.apache.xmlgraphics:batik-svggen:${ver_batik}") {
		exclude group: 'xml-apis', module: 'xml-apis'
	}
	implementation "org.apache.xmlgraphics:batik-util:${ver_batik}"
	implementation("org.apache.xmlgraphics:batik-xml:${ver_batik}") {
		exclude group: 'xalan', module: 'xalan'
		exclude group: 'xml-apis', module: 'xml-apis'
	}
}

defaultTasks 'clean', 'distributions'

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = sourceCompatibility
	modularity.inferModulePath = false
}
sourceSets {
	main.java.srcDirs = ['src/java']
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	modularity.inferModulePath = false
}

version = '1.0'
group = 'org.tiago'
description = 'Lositan and Mcheza - Detection of genes under selection.'

ext {
	moduleName = 'lositan'
	depsDirName = 'lib'
	title = "${name}: ${description}".toString()
	author = 'Tiago Ant√£o'
	email = 'tiago@tiago.org'
	copyright = "2017-${java.time.LocalDate.now().year} ${author}".toString()
	url = 'https://tiago.org/'
	licenseType = 'GNU General Public License'
	mainClassName = 'Boot'
	defaultJvmArguments = ["-Xmx1024m"]
	tokenMap = [
		'app.title': name.toUpperCase(),
		'name': name,
		'version': version,
	]
}

tasks.register('packagePythonSource', Zip) {
	group = 'build'
	description 'Assembles a zip archive containing the deployment Python source.'
	archiveFileName = 'py.zip'
	destinationDirectory = file("${buildDir}/tmp")
	from "${projectDir}/src/python"
}

tasks.register('packageBioPython', Copy) {
	group = 'build'
	description 'Assembles a folder containing a minimal BioPython distribution.'
	def dest = file("${buildDir}/tmp/Lib")
	def zipPath = project.configurations.biopython.find { it.name.startsWith("biopython") }
	from(zipTree(zipPath)) {
		include "*/LICENSE"
		include "*/README"
		include "*/Bio/__init__.py"
		include "*/Bio/File.py"
		include "*/Bio/ParserSupport.py"
		include "*/Bio/_py3k/**/*"
		include "*/Bio/PopGen/**/*"
		eachFile { it.relativePath = new RelativePath(true, it.relativePath.segments.drop(1)) }
		includeEmptyDirs = false
	}
	into dest
}

jar {
	inputs.property('moduleName', moduleName)
	from packagePythonSource
	from "${projectDir}/support/allfdist.zip"
	from("${projectDir}/support") {
		include '*.gp'
		include '*.txt'
		include '*.temp'
	}
	manifest {
		attributes += [
			'Build-Jdk': System.getProperty('java.version'),
			'Created-By': "Gradle ${gradle.gradleVersion}",
			'Automatic-Module-Name': "${project.group}.${moduleName}",
			'Class-Path': configurations.runtimeClasspath.collect { it.name }.join(' ') + ' Lib/',
			'Main-Class': project.mainClassName,
		]
	}
}

javadoc {
	options {
		addBooleanOption('Xdoclint:all,-missing', true)
	}
}

tasks.register('processScriptTemplates', Copy) {
	description 'Processes start script templates.'
	from("${projectDir}/src/templates") {
		filter(ReplaceTokens, tokens: tokenMap, beginToken: '${', endToken: '}')
	}
	into "${buildDir}/script-templates"
}

application {
	mainClass = project.mainClassName
	executableDir = ''
	applicationDefaultJvmArgs = project.defaultJvmArguments
}

tasks.register('createStartScriptLositemp', CreateStartScripts) {
	description = 'Creates OS specific scripts (for temporal version of application).'
	dependsOn processScriptTemplates
	applicationName = 'lositemp'
	mainClass = project.mainClassName
	defaultJvmOpts = project.defaultJvmArguments
	outputDir = new File(buildDir, 'scripts')
	unixStartScriptGenerator.template = resources.text.fromFile("${buildDir}/script-templates/script-template-lositemp.sh")
	windowsStartScriptGenerator.template = resources.text.fromFile("${buildDir}/script-templates/script-template-lositemp.bat")
	doLast {
		unixScript.text = unixScript.text.replaceAll('\'"(.+?)"\'', '$1')
	}
}

tasks.register('createStartScriptMcheza', CreateStartScripts) {
	description = 'Creates OS specific scripts (for Mcheza/dominant version of application).'
	dependsOn processScriptTemplates
	applicationName = 'mcheza'
	mainClass = project.mainClassName
	defaultJvmOpts = project.defaultJvmArguments
	outputDir = new File(buildDir, 'scripts')
	unixStartScriptGenerator.template = resources.text.fromFile("${buildDir}/script-templates/script-template-mcheza.sh")
	windowsStartScriptGenerator.template = resources.text.fromFile("${buildDir}/script-templates/script-template-mcheza.bat")
	doLast {
		unixScript.text = unixScript.text.replaceAll('\'"(.+?)"\'', '$1')
	}
}

startScripts {
	dependsOn processScriptTemplates, createStartScriptLositemp, createStartScriptMcheza
	unixStartScriptGenerator.template = resources.text.fromFile("${buildDir}/script-templates/script-template.sh")
	windowsStartScriptGenerator.template = resources.text.fromFile("${buildDir}/script-templates/script-template.bat")
	doLast {
		unixScript.text = unixScript.text.replaceAll('\'"(.+?)"\'', '$1')
	}
}

tasks.register('distributions') {
	dependsOn startScripts, installDist, assembleDist
	group = 'distribution'
	description = 'Assembles all standard distributions.'
}

distributions {
	main {
		contents {
			from(packageBioPython) {
				into 'lib/Lib'
			}
			from("${projectDir}/mcheza.jpg") {
				into 'lib'
			}
			from("${projectDir}/title.gif") {
				into 'lib'
			}
		}
	}
}
distTar.enabled = false
